apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

apply plugin: 'idea'
apply plugin: 'eclipse'


defaultTasks 'build'

sourceCompatibility = 1.6
group = 'com.srikalyan'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-logging:commons-logging:1.2'

    testCompile 'junit:junit:4.11'
    testCompile 'org.easymock:easymockclassextension:3.2'
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name, 'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DNS cache revoker'
                description 'Addresses Java issue which does not honor dns ttls'
                url 'https://github.com/srikalyan/dns_cache_revoker'
                packaging 'jar'

                scm {
                    url 'https://github.com/srikalyan/dns_cache_revoker'
                    connection 'scm:https://srikalyan@github.com/srikalyan/dns_cache_revoker.git'
                    developerConnection 'scm:git://github.com/srikalyan/dns_cache_revoker.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'srikalyan'
                        name 'Srikalyan Swayampakula'
                        email 'srikalyan.swayampakula@gmail.com'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}